apply plugin: 'com.android.model.application'

def ndkDir = System.getenv("ANDROID_NDK_HOME")
def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())
    ndkDir = properties.getProperty('ndk.dir')
}

def ndkInc = ndkDir + "/sysroot/usr/include"

model {

    repositories {
        libs(PrebuiltLibraries) {
        }
    }

    android {
        compileSdkVersion = 25
        buildToolsVersion = '25.0.2'

        defaultConfig{
            applicationId = "igagis.org.mordademo"
            minSdkVersion.apiLevel = 21
            targetSdkVersion.apiLevel = 21
            versionCode = 1
            versionName = '1.0'
        }

        buildTypes {
            debug {
                ndk {
                    cppFlags.add("-DDEBUG")
                }
            }
        }

        ndk {
            platformVersion = 21
            moduleName = 'mordavokneapp'//this has to be 'mordavokneapp' so that MordaVOkneActivity loads that named .so
            toolchain = 'clang'
            stl = 'c++_shared'
            cppFlags.add("-std=c++11")
            cppFlags.add("-fexceptions")
            cppFlags.add("-frtti")
            cppFlags.add("-g")
            cppFlags.add("-fintegrated-as") //needed to avoid assembler error since NDK r15
            cppFlags.add("-I" + projectDir.getAbsolutePath() + "/build/ndkLibs/include")
            cppFlags.addAll(["-isystem", ndkInc])
            cppFlags.add("-D__ANDROID_API__=" + platformVersion)
            cppFlags.add("-DANDROID_STL=c++_shared")
            ldLibs.addAll(['android', 'log',
                           'mordavokne',
                           'EGL', 'morda-opengles2-ren',
                           'morda',
                           'nitki',
                           'stob', 'papki',
                           'png', 'jpeg', 'GLESv2', 'z', 'freetype2', 'svgren', 'svgdom',
                           'mikroxml', 'unikod', 'cairo', 'pixman',
                           'c++'])
            // build a default combined apk including all ABIs.
            abiFilters.addAll(['armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'])
        }
        abis {
            create("armeabi-v7a") {
                ldFlags.add("-L" + projectDir.getAbsolutePath() + "/build/ndkLibs/armeabi-v7a")
                cppFlags.addAll(["-isystem", ndkInc + "/arm-linux-androideabi"])
            }
            create("arm64-v8a") {
                ldFlags.add("-L" + projectDir.getAbsolutePath() + "/build/ndkLibs/arm64-v8a")
//                ldFlags.add("-L" + ndkDir + "/platforms/" + platformVersion + "/arch-arm64")
                cppFlags.addAll(["-isystem", ndkInc + "/aarch64-linux-android"])
            }
            create("x86") {
                ldFlags.add("-L" + projectDir.getAbsolutePath() + "/build/ndkLibs/x86")
                cppFlags.addAll(["-isystem", ndkInc + "/i686-linux-android"])
            }
            create("x86_64") {
                ldFlags.add("-L" + projectDir.getAbsolutePath() + "/build/ndkLibs/x86_64")
                cppFlags.addAll(["-isystem", ndkInc + "/x86_64-linux-android"])
            }
        }
        sources {
            main {
                jni {
                    dependencies {
                    }
                    source {
                        srcDirs '../../src'
                    }
                }
                jniLibs {
                    // for shared lib, lib need to be pushed to the target too
                    // Once libs are copied into app/src/main/jniLibs directory,
                    // Android Studio will pack them into APK's lib/ directory
                    // Here we like to avoid another duplication by pointing
                    // to the files that containing our libs' distribution location
                    // so the same file is used by compiler at host, also packed
                    // into APk to be used at Target (phone/tablet)
                    source {
                    }
                }
            }
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles.add(file('proguard-rules.pro'))
            }
        }
    }

    android.lintOptions {
        abortOnError false
    }
}

repositories {
    maven {
        url 'https://dl.bintray.com/igagis/android/'
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'io.github.igagis:libmordavokne:+'
}



task extractNDKLibs {
    doLast {
        configurations.compile.each {
            def file = it.absoluteFile
            copy {
                from zipTree(file)
                into "build/"
                include "ndkLibs/**/*"
            }
        }
    }
}
build.dependsOn('extractNDKLibs')
tasks.whenTaskAdded { task ->
    if (task.name.startsWith('compile')) {
        task.dependsOn('extractNDKLibs')
    }
}

task copyResToAssets {
    doLast {
        copy{
            from "../../res"
            into "src/main/assets/res"
            include "**/*"
        }
    }
}
tasks.whenTaskAdded { task ->
    if (task.name.startsWith('generate')) {
        task.dependsOn('copyResToAssets')
    }
}